AC_INIT(configure.in)

VERSION=0.2.99.4
PACKAGE=g3dviewer

AM_CONFIG_HEADER(config.h)

AM_INIT_AUTOMAKE($PACKAGE, $VERSION, no-define)
AC_SUBST(VERSION)

AC_PROG_CC

ALL_LINGUAS="de"
AM_GLIB_GNU_GETTEXT

GETTEXT_PACKAGE=$PACKAGE
AC_SUBST(GETTEXT_PACKAGE)

# glib 2.0
AM_PATH_GLIB_2_0(2.4.0,,AC_MSG_ERROR([GLib >= 2.4.0 is required]))

# gtk+ 2.0
AM_PATH_GTK_2_0(2.4.0,,AC_MSG_ERROR([GTK+ >= 2.4.0 is required]))

# libglade 2.0
PKG_CHECK_MODULES(LIBGLADE, libglade-2.0 >= 2.4.0,,
	AC_MSG_ERROR([libglade >= 2.4.0 is required]))

# gtkglext
AM_PATH_GTKGLEXT_1_0(1.0.0,,AC_MSG_ERROR([GtkGlExt >= 1.0.0 is required]))

# libg3d
PKG_CHECK_MODULES(LIBG3D, libg3d >= 0.0.5,,
	AC_MSG_ERROR([libg3d >= 0.0.5 is required]))

# Check OpenGL stuff
AC_PATH_XTRA

ac_save_CPPFLAGS="$CPPFLAGS"
CPPFLAGS="$CPPFLAGS $X_CFLAGS"

ac_save_LIBS="$LIBS"
LIBS="$LIBS $X_LIBS"

AC_CHECK_HEADERS(GL/gl.h GL/glu.h,,
	AC_MSG_ERROR([some OpenGL headers missing]),
	[#include <GL/gl.h>])

AC_CHECK_LIB(GL, glBegin, have_gl=yes, have_gl=no)
AC_CHECK_LIB(GLU, gluPerspective, have_glu=yes, have_glu=no, -lGL $GTK_LIBS)
if test $have_gl = no || test $have_glu = no; then
	AC_MSG_ERROR([libgl or libglu missing])
else
	GL_LIBS="-lGL -lGLU $GTK_LIBS"
	AC_SUBST(GL_LIBS)
fi

# Check some UNIX headers
AC_CHECK_HEADERS(dirent.h sys/stat.h unistd.h,,
	AC_MSG_ERROR([missing header file]),
	[#include <sys/types.h>])

# Look for snprintf (optional)
AC_CHECK_FUNC(snprintf,[AC_DEFINE(HAVE_SNPRINTF,1,[snprintf])],,stdio.h)

# Look for fstat (required)
AC_CHECK_FUNC(fstat, have_fstat=yes, have_fstat=no, [unistd.h sys/types.h])
if test $have_fstat = no; then
	AC_MSG_ERROR([fstat function not found])
fi

# Debug level (verbosity & optimization)
AC_MSG_CHECKING([for debug level])
AC_ARG_ENABLE(debug, 
  [[  --enable-debug[=LVL]    enable debug level LVL or 1 (default is 0)]],
  [if test "$enableval" = "yes" ; then DEBUG=1; else DEBUG=$enableval; fi],
  DEBUG=0)
AC_MSG_RESULT($DEBUG)
AC_SUBST(DEBUG)

if test "$DEBUG" = "0"; then
	DBG_CFLAGS="-O2"
else
	DBG_CFLAGS="-g -DGLIB_DISABLE_DEPRECATED -DGTK_DISABLE_DEPRECATED -DGDK_DISABLE_DEPRECATED"
fi
AC_SUBST(DBG_CFLAGS)

# Data & plugin directories
AC_DEFINE_DIR(DATA_DIR,   datadir/$PACKAGE, [Data directory])

# Create the files
AC_OUTPUT(Makefile
	m4/Makefile
	man/Makefile
	src/Makefile
	thumbnailer/Makefile
	pixmaps/Makefile
	glade/Makefile
	po/Makefile.in
)
	

